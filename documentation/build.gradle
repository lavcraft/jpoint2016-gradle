buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'org.asciidoctor:asciidoctorj:1.5.4'
  }
}

plugins {
  id 'org.asciidoctor.convert' version '1.5.3'
}

configurations {
  docs
}

apply plugin: 'org.asciidoctor.convert'

List<String> docsSources = fileTree(dir: projectDir, include: '**/*.adoc')
    .toList() // hack for ruby. Prevent NoSuchMethodException exception
    .collect { relativePath(it) }

asciidoctorj {
  version = '1.5.4'
  groovyDslVersion = '1.0.0.Alpha1'
}

asciidoctor {
  dependsOn 'prepare'
  sourceDir project.projectDir

  sources {
    setIncludes docsSources
  }

  outputDir = new File("$buildDir/docs")
  attributes(['source-highlighter': 'coderay',
              doctype             : 'book',
              toc                 : 'left',
              idprefix            : '',
              idseparator         : '-'])

  afterEvaluate {
    org.asciidoctor.extension.DependencyIncludeProcessor.project = project
  }
}

dependencies {
  asciidoctor 'org.asciidoctor:asciidoctorj:1.5.4'
  docs project(path: ':service-with-deps', configuration: 'documentationConfiguration')
}

task copyDist(type: Copy) {
  into "$buildDir/docsArchive"
  from configurations.docs
}

task prepare(type: Copy) {
  dependsOn copyDist
  into "$buildDir/docsDependencies/"
  configurations.docs.files.each { archive ->
    into(archive.name) {
      from zipTree(archive)
    }
  }
}

task dist(type: Tar) {
  dependsOn 'asciidoctor'
  compression 'gzip'
  from file(asciidoctor.outputDir)
}

artifacts {
  documentationConfiguration dist
}
